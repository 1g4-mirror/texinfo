# @configure_input@
#
# Add directories to @INC, Perl's module search path, to find modules,
# either in the source or build directories.
#
# 'use Texinfo::ModulePath' should only occur once per program so that
# the initialization is only done once.  This makes the way this module
# is used very unusual.
#
# Also used to pass other information from configure to modules.

package Texinfo::ModulePath;

use vars qw($VERSION);

$VERSION = '7.2dev';

use File::Basename;
use File::Spec;

# Used as a flag to say whether to look for data files in uninstalled
# locations.
our $texinfo_uninstalled = undef;

# uninstalled locations paths, if $Texinfo::ModulePath::texinfo_uninstalled
# Pathname of the tp/ build and source directories.  Used to find the locale
# data and other files.
our $tp_builddir = '';
our $tp_srcdir;

# installed locations paths, if not $Texinfo::ModulePath::texinfo_uninstalled
our $converterdatadir;
our $converterxsdir;

# Other information passed to modules from configure
our $conversion_from_euc_cn = '@ICONV_CONVERTS_EUC_CN@';

# If $LIB_DIR and $LIBEXEC_DIR are given,
# (likely the installation directories)
# use them to add directories
# to @INC.
#
# LIB_DIR is for bundled libraries.
# LIBEXEC_DIR is for XS modules.
# CONVERTERDATADIR is for HTML extensions, javascript
#
# otherwise use 'tp_srcdir'
# and 'tp_builddir' environment variables.
sub init {
  my $lib_dir = shift;
  $converterxsdir = shift;
  $converterdatadir = shift;
  my %named_args = @_;

  if (!$named_args{'installed'}) {
    $texinfo_uninstalled = 1;

    if ($ENV{'tp_srcdir'}) {
      $tp_srcdir = $ENV{'tp_srcdir'};
    } elsif (defined $named_args{'updirs'}) {
      # the command used is always in the source directory
      my ($real_command_name, $command_directory, $command_suffix)
              = fileparse($0, '.pl');
      my $updir = File::Spec->updir();

      # e.g. tp/t -> tp/t/../.. for 'updirs' = 2.
      my $count = $named_args{'updirs'};
      $tp_srcdir = $command_directory;
      while ($count-- > 0) {
        $tp_srcdir = join('/', ($tp_srcdir, $updir));
      }
    }

    if (defined($tp_srcdir)) {
      # For Texinfo::Parser and the rest.
      unshift @INC, "$tp_srcdir";
      $lib_dir = join('/', ($tp_srcdir, 'maintain'));
    }

    # Find XS modules in the build directory
    if (defined($ENV{'tp_builddir'})) {
      $tp_builddir = $ENV{'tp_builddir'};
    } else {
      # this is correct for in-source builds only.
      $tp_builddir = $tp_srcdir;
    }
    if (defined($tp_builddir)) {
      unshift @INC, join('/', ($tp_builddir, 'Texinfo', 'XS'));
      unshift @INC, join('/', ($tp_builddir, 'C'));
    }

  } else {
    $texinfo_uninstalled = 0;

    # for the Texinfo modules
    if (defined($lib_dir)) {
      unshift @INC, $lib_dir;
    }
    # for the XS libraries
    if (defined($converterxsdir)) {
      unshift @INC, $converterxsdir;
    }
  }

  if (defined($lib_dir)) {
    # Bundled libraries
    # '@USE_EXTERNAL_LIBINTL @' and similar are substituted
    if ('@USE_EXTERNAL_LIBINTL@' ne 'yes') {
      unshift @INC, join('/', ($lib_dir, 'lib', 'libintl-perl', 'lib'));
    }
    if ('@USE_EXTERNAL_EASTASIANWIDTH@' ne 'yes') {
      unshift @INC, join('/', ($lib_dir, 'lib', 'Unicode-EastAsianWidth', 'lib'));
    }
    if ('@USE_EXTERNAL_UNIDECODE@' ne 'yes') {
      unshift @INC, join('/', ($lib_dir, 'lib', 'Text-Unidecode', 'lib'));
    }
  }
}

sub import {
  my $class = shift;
  goto &init;
}

1;
